* bra:   0
* ket:   1
* lt:    2
* gt:    3
* plus:  4
* minus: 5
* comma: 6
* dot:   7
* pipe:  minus 1 ( end of source )

* load source
*_ec
->>->>>
e+[-
 *eof
                          e+>c,+[<e-]>[c>] <<e[-<-      >]
 *plus
 > c[<e++++++[->c-------<]e+>c--[<e-]>[c>]+<<e[-<++++   >]]>>[->]
 *comma
 <<c[<                    e+>c- [<e-]>[c>]+<<e[-<++++++ >]]>>[->]
 *minus
 <<c[<                    e+>c- [<e-]>[c>]+<<e[-<+++++  >]]>>[->]
 *dot
 <<c[<                    e+>c- [<e-]>[c>]+<<e[-<+++++++>]]>>[->]
 *lt
 <<c[<e++[->c-------<]    e+>c  [<e-]>[c>]+<<e[-<++     >]]>>[->]
 *gt
 <<c[<                    e+>c--[<e-]>[c>]+<<e[-<+++    >]]>>[->]
 *bra
 <<c[<e+++++[->c------<]    >c+      >    +<<e            ]>>[->]
 *ket
 <<c[<                    e+>c--[<e-]>[c>]+<<e[-<+      >]]>>[->]
 *pipe(end of source)
 <<c[<e+++++[->c------<]  e+>c- [<e-]>[c>]+<<e[-<-      >]]>>[->]
 <+<c[c---[+]>e-<c]>[-<+<+>>]<<<+[->]->[e->c--->]<++
ec]
>>-<<<<<+[-<+]->>
* execute
+[-<
 * case 0: bra
 f    +>X      [      <->[->+<]]<[-->      +[->+]-
  >>>f+<+<b[>>f->+[->>>>+]->>]>>[f->+[->>>>+]->>]+<
  [>-<<<<<<+[-<<+]>>-]>[-<<<<<<+[-<<+]>>  >]
  <<<->>[-
   <<<+[-<<+]>>-
   *[
    +>>-
    <X[-[*other
     X+<<Y[-<+>]
    ]>[*ket#
     +<X+<-<+[-<<+]-
     <<+[
      ->>>+[->>+]>>-<<-<+[-<<+]-<
      *sub*
      -[++<-]<<+[[->+]>-<]>+[-->+[->+]>]<-
      <+[-<+]
     ]->+[->+]->+[->>+]
     <Y[-<+>]>
    ]<]>[*bra
     <<<<-<+[-<<+]-
     *add*
     <[-<]+<+[[->+]>-<]>+[<<->>[->+]>]<-
     >+[->>+]>Y[-<+>]>
    ]
    <<[->+<]
   >>>>]
   <<->+[->>+]->>
  ]<<
 <+[-<+]] >>[-<+<+>>]<<
 * case 1: ket
 f[[-]+>X-     [+     <->[->+<]]<[-->+     +[->+]-
  >>>f+<+<b[>>f->+[->>>>+]->>]>>[f->+[->>>>+]->>]+<
  [>-<<<<<<+[-<<+]>>-]>[-<<<<<<+[-<<+]>>-->]
  <+<<->>[-
   <<<+[-<<+]-
   *[
    +<<-
    >X[-[*other
     X+>>Y[->+<]
    ]<[*ket
     >X+<<+[-<<+]-
     *add*
     <[-<]+<+[[->+]>-<]>+[<<->>[->+]>]<-
     >+[->>+]->>>Y[->+<]<
    ]>]<[*bra
     <+[-<<+]-
     <<+[
      ->>>+[->>+]>>+<<-<+[-<<+]-<
      *sub*
      -[++<-]<<+[[->+]>-<]>+[-->+[->+]>]<-
      <+[-<+]
     ]->+[->+]->+[->>+]
     >>[-<<->>]>Y[->+<]<
    ]
    >>[-<+>]
   <<<<]
   ->+[->>+]->>
  ]<<
 <+[-<+]]]>>[-<+<+>>]<<
 * case 2: lt
 f[[-]+>X--    [++    <->[->+<]]<[-->++    +[->+]-
  >>>f+<<b[
   >>->+[->>>>+]>>>>-<<<<+[-<<<<+]
  ]>>[
   f>+[<f->[->>>>+]<<<<-<<<<+[-<<<<+]>>>f]
   <[f->-<<<b+<+>>]
  ]<<-
 <+[-<+]]]>>[-<+<+>>]<<
 * case 3: gt
 f[[-]+>X---   [+++   <->[->+<]]<[-->+++   +[->+]-
  >>>f+<<b[
   >>>+[<f->[->>>>+]<<<<-<<<<+[-<<<<+]>>>f]
   <[f->-<<<b-<+>>]<<
  ]>>[
   f->+[->>>>+]>>>>-<<<<+[-<<<<+]>>
  ]<<-
 <+[-<+]]]>>[-<+<+>>]<<
 * case 4: plus
 f[[-]+>X----  [++++  <->[->+<]]<[-->++++  +[->+]-
  >>>f+< <b[>>f->+[->>>>+]->>]>>[f->+[->>>>+]->>] <
  +
  <<<<<+[-<<+]-
 <+[-<+]]]>>[-<+<+>>]<<
 * case 5: minus
 f[[-]+>X----- [+++++ <->[->+<]]<[-->+++++ +[->+]-
  >>>f+< <b[>>f->+[->>>>+]->>]>>[f->+[->>>>+]->>] <
  -
  <<<<<+[-<<+]-
 <+[-<+]]]>>[-<+<+>>]<<
 * case 6: comma
 f[[-]+>X------[++++++<->[->+<]]<[-->+++++++[->+]-
  >>>f+< <b[>>f->+[->>>>+]->>]>>[f->+[->>>>+]->>] <
  ,
  <<<<<+[-<<+]-
 <+[-<+]]]>>[-<+<+>>]<<
 * case 7: dot
 f[[-]                             ->      +[->+]-
  >>>f+< <b[>>f->+[->>>>+]->>]>>[f->+[->>>>+]->>] <
  .
  <<<<<+[-<<+]-
 <+[-<+] ]
>>>+]
